# 情報ログ形式でecho
# info 'Hello World!'
# info 'test \e[33m yellw'  途中から色を変えることも可能
function info() {
    #echo -e "\e[32m[\e[34mINFO\e[32m] \e[34m$1\e[m"
    printf "\e[32m[\e[34mINFO\e[32m] \e[34m$1\e[m\n"
}
function warn() {
    printf "\e[32m[\e[33mWARN\e[32m] \e[34m$1\e[m\n"
}
function error() {
    printf "\e[32m[\e[31mERROR\e[32m] \e[34m$1\e[m\n"
}

# 確認ダイアログ越しにコマンド実行
# confirm echo Hello World!
function confirm() {
    CMD=$@
    _lazy_install gum
    if type gum > /dev/null 2>&1; then
        gum confirm && eval $CMD || info 'cancel'
    else
        if [ -n "$ZSH_VERSION" ]; then
            read "input?Are you sure? (y/n):"
        else
            read -p "Are you sure? (y/n):" input
        fi
        if [ "$input" = "y" ]; then
            eval $CMD
        else
            info "cancel"
        fi
    fi
}

# 標準出力を出さずに、ロード中spinを表示
# spin 'loading...' sleep 3
function spin() {
    _lazy_install gum
    gum spin --title "$1" -- "${@:2}"
}

# vim
function v() {
    _lazy_install fzf
    _lazy_install_ripgrep
    # deactivating vim-IDE-menu
    #_lazy_install silicon
    #_lazy_install code-minimap
    if [ ! -d ~/.vim/plugged ]; then
        # vim-plugがない場合
        spin 'install vim-plug' \
            curl -fSsLo ~/.vim/autoload/plug.vim --create-dirs \
            https://raw.githubusercontent.com/junegunn/vim-plug/master/plug.vim
        vim -c "PlugInstall"
    else
        vim -c "CocCommand explorer --no-focus --width 30" $@
    fi
}

# zoxide
# eza fzf bat
function _zoxide() {
    _lazy_install zoxide
    _lazy_install eza
    _lazy_install fzf
    _lazy_install bat
    eval "$(zoxide init zsh)"
    zi
}
export _ZO_FZF_OPTS='
    --no-sort --height 75% --reverse --margin=0,1 --exit-0 --select-1
    --bind ctrl-f:page-down,ctrl-b:page-up
    --bind pgdn:preview-page-down,pgup:preview-page-up
    --prompt="❯ "
    --color bg+:#262626,fg+:#dadada,hl:#f09479,hl+:#f09479
    --color border:#303030,info:#cfcfb0,header:#80a0ff,spinner:#36c692
    --color prompt:#87afff,pointer:#ff5189,marker:#f09479
    --preview "([[ -e '{2..}/README.md' ]] && bat --color=always --style=numbers --line-range=:50 '{2..}/README.md') || eza --color=always --group-directories-first --oneline {2..}"
'
alias zi='_zoxide'
alias zv='_zoxide && v'

# terminal ctrl
alias rm='rm -i'
alias rmf='confirm rm -rf'
alias re='exec $SHELL -l'
alias q='exit'

# ls
function l() {
    _lazy_install eza
    if type eza > /dev/null 2>&1; then
        if [ "$(uname)" = "Darwin" ]; then
            eza -abghHliS --icons --git $@
        else
            eza -abghHliS $@
        fi
    else
        if [ "$(uname)" = "Darwin" ]; then
            ls -AFGlihrt --color=auto $@
        else
            ls -AFlihrt --color=auto $@
        fi
    fi
}
function tree() {
    _lazy_install eza
    if type eza > /dev/null 2>&1; then
        if [ "$(uname)" = "Darwin" ]; then
            eza -bghHliST --icons --git $@
        else
            eza -bghHliST $@
        fi
    else
        pwd;find . | sort | sed '1d;s/^\.//;s/\/\([^/]*\)$/|--\1/;s/\/[^/|]*/|  /g'
    fi
}

# TUI
function e() {
    _lazy_install_yazi
    yazi
}

function db() {
    _lazy_install_gobang
    if [ $# -eq 0 ]; then
        gobang
        return
    fi
    if [ "$1" = "edit_con" ]; then
        cd ~/git/dotfiles && v conf/config/gobang/config.toml
    fi
}
function _db() { _values '' 'edit_con' }
compdef _db db

function g() {
    _lazy_install git
    _lazy_install lazygit
    lazygit
}

function d() {
    _lazy_install lazydocker
    CNT=$(ps aux | grep -c OrbStack)
    if [ $CNT -eq 1 ]; then
        info 'START OrbStack'
        open -g /Applications/OrbStack.app
        sleep 1
        _lazy_install genact
        genact -s 10 --exit-after-modules 1 -m botnet
        genact -s 10 --exit-after-modules 1 -m bruteforce
    fi
    lazydocker
}

function c() {
    _lazy_install cmatrix
    cmatrix
}

# バックiサーチをhistoryのfzfにする
function incremental_search_history() {
    _lazy_install fzf
    selected=`history -E 0 | fzf --tac --height 75% --reverse --margin=0,1 | cut -b 24-`
    BUFFER=`[ ${#selected} -gt 0 ] && echo $selected || echo $BUFFER`
    CURSOR=${#BUFFER}
    zle redisplay
}
zle -N incremental_search_history
bindkey "^r" incremental_search_history

# others
function gif() {
    _lazy_install ffmpeg
    _lazy_install gum
    ffmpeg -i $(ls -A | gum choose) -r 10 $(gum input --prompt 'e.g.) test.gif: ')
}

function s() {
    _lazy_install gum
    if [ "$1" = "ssh" ]; then
        if [ ! -f ~/.ssh/known_hosts ]; then
            ssh $(gum input --prompt 'target. e.g.) github.com: ')
        else
            ssh $(cat ~/.ssh/known_hosts | awk '{print $1}' | uniq | gum choose)
        fi
    fi
    if [ "$1" = "scp" ]; then
        _lazy_install_termscp
        termscp
    fi
}
function _s() { _values '' 'ssh' 'scp' }
compdef _s s

# forgeコマンドと補完
if not_has forge; then
    gh api -H "Accept: application/vnd.github.raw" -H "X-GitHub-Api-Version: 2022-11-28" "/repos/serna37/forge/contents/forge.sh?ref=main" > /usr/local/bin/forge && chmod +xw /usr/local/bin/forge
fi
function _forge() {
    local -a val
    val=(
      'gen[generate a new repository and init deploy]'
      'init-deploy[init deploy]'
      'run[run containers on local]'
      'stop[stop containers on local]'
      'release[develop -> release -> master]'
      'upgrade[upgrade forge]'
      '-h[show help]'
    )
    _values '' $val
}
compdef _forge forge

function Azathoth() {
    _lazy_install gum
    if ! gum confirm; then
        return
    fi
    brew list
    \rm -rf ~/.nvm
    \rm ~/.netrc
    \rm /usr/local/bin/forge
    DEL=(fastfetch screenfetch
    zoxide fzf bat eza ripgrep
    lazygit lazydocker
    genact cmatrix tty-clock
    yazi gobang termscp gum
    ffmpeg jq
    zsh-syntax-highlighting zsh-autosuggestions
    zsh-git-prompt powerlevel10k
    )
    for v in ${DEL[@]}; do
        brew uninstall $v
    done
    exec $SHELL -l
}

